plugins {
    id 'com.diffplug.spotless' version '6.11.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.ajanuary'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.11'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror'
}

tasks.named('test') {
    useJUnitPlatform()
}

pitest {
    targetClasses = ['com.ajanuary.openenum.*']
    junit5PluginVersion = '0.15'
}

spotless {
    java {
        googleJavaFormat()
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (System.getenv().containsKey('SIGNING_KEY_ID')) {
    signing {
        useInMemoryPgpKeys(
                System.getenv("SIGNING_KEY_ID"),
                System.getenv("SIGNING_KEY"),
                System.getenv("SIGNING_PASSWORD"))
        sign configurations.archives
    }
}

publishing {
    publications {
        mavenJava(MavenPublication)
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}